
import tutorial;

// Class declaration syntax
//
// <modifier_list> class <name> [extends <name>]
// {
//   <class_content_list>
// }
//
// <modifier> -> abstract - more in inheritance
// <modifier> -> final    - more in inheritance
//
// <class_content> -> <member>
// <class_content> -> <method>
//

class Demo
{
  // Dummy private member
  member = 0;

  // Class constructor Demo#0
  public Demo() {}

  // Class constructor Demo#1
  public Demo(value)
  {
    member = value;
  }

  // Simple identification method
  public to_string()
  {
    return "Demo(%d)" % member;
  }
}

class Main
{
  static public main(argv)
  {
    // Create new object of class Demo
    demo = new Demo();

    Tutorial.note("Instance of class Demo was created by constructor Demo#0");
    ("demo: %s\n" % demo.to_string()).print();

    // Create new object of class Demo
    demo_1 = new Demo(1);

    Tutorial.note("Instance of class Demo was created by constructor Demo#1");
    ("demo_1: %s\n" % demo_1.to_string()).print();

    // Create multiple instances of class Demo
    demos = new Demo[5];

    Tutorial.note("Multiple instances of class Demo were created by constructor Demo#0");
    ("demos: %s\n" % demos.to_string()).print();

    Tutorial.note("Tutorial end");
  }
}

