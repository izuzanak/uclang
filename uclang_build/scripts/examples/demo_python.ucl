
import containers;
import python;
import sys;
import json;

class Main
{
  // - print information about person -
  public person_info(a_person)
  {
    return "%s %s" % [a_person.name.to_string(),a_person.surname.to_string()];
  }

  public Main()
  {
    sys = PyObject.module("sys");
    json = PyObject.module("json");

    // - update python import path -
    sys.path.append("%s/scripts/examples/" % Sys.getcwd());

    // - load demo module -
    demo = PyObject.module("demo");

    // - module demo properties -
    ("demo properties: %s\n" % demo.dir().value().to_string()).print();

    // - retrieve person from list -
    avone = demo.persons["avone"];
    ("person: %s\n" % person_info(avone)).print();
    ("persons: %s\n" % demo.person_list().value().to_string()).print();

    // - add person to list -
    frank = demo.Person("Frank","Sobotka");
    demo.persons["frank"] = frank;
    ("person: %s\n" % person_info(frank)).print();
    ("persons: %s\n" % demo.person_list().value().to_string()).print();

    // - create persons JSON in python-
    python_json = demo.json(json,demo.person_dict());
    ("python_json: %s\n" % python_json.value()).print();

    // - create persons JSON in uclang-
    ucl_json = Json.create(demo.person_dict().value());
    ("ucl_json:    %s\n" % ucl_json).print();
  }

  static public main(argv)
  {
    new Main();
  }
}

